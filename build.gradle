

group = 'edu.sc.seis'
version = '1.5.2-SNAPSHOT'

apply plugin: 'maven'

dependencies {
    compile project(':seedCodec')
    compile "org.slf4j:slf4j-api:1.6.6"
    runtime group: "org.slf4j", name: "slf4j-log4j12", version: "1.6.6", transitive: false
    compile "javax.xml.stream:stax-api:1.0-2"
    runtime "org.codehaus.woodstox:woodstox-core-lgpl:4.1.0"
    runtime "mysql:mysql-connector-java:5.1.21"
}

def binDistFiles = copySpec {
    from('build/output') {
        fileMode=0755
        include('bin/**')
    }
    from('build/output') {
        include('lib/**')
    }
}

def distFiles = copySpec {
    with(binDistFiles)
    from('build/docs') {
        include('javadoc/**')
    }
    from('build') {
        include('build.gradle')
        include('settings.gradle')
    }
    from('.') {
        include('gpl-3.0.txt')
        include('doc/**')
        include('src/**')
        include('gradle/**')
        include('gradlew')
        include('gradlew.bat')
        exclude('**/*.svn')
    }
    from('.') {
        fileMode=0755
        include('gradlew')
    }
    from('../seiswww/build/site') {
        include('seisFile.html')
    }
    from('build/generated-src/modVersion') {
        include('java/**')
        into('src/main')
    }
}


task explodeBin(type: Sync, dependsOn: ['makeScript' ]) {
    group = 'dist'
    with binDistFiles
    into file("$buildDir/explode")
}

task explodeDist(type: Sync, dependsOn: ['explodeBin', 'javadoc', 'modVersionClass', 'createBuildScript', ':seiswww:makeSite', 'wrapper']) {
    group = 'dist'
    with distFiles
    into file("$buildDir/explode")
}

task tarDist(type: Tar, dependsOn: ['explodeDist' ]) {
    def dirName = project.name+'-'+version
    compression = Compression.GZIP
    into(dirName) {
        with distFiles
    }
}

task makeScript(dependsOn: copyToLib) << { task ->
    group = 'build'
    def dExtras = getDExtras()
    def doBat = true
    ScriptBuilder.create('saclh', 'edu.sc.seis.seisFile.sac.ListHeader', task.project, dExtras:dExtras, bat:doBat)
    ScriptBuilder.create('mseedlh', 'edu.sc.seis.seisFile.mseed.ListHeader', task.project, dExtras:dExtras, bat:doBat)
    ScriptBuilder.create('seedlinkclient', 'edu.sc.seis.seisFile.seedlink.Client', task.project, dExtras:dExtras, bat:doBat)
    ScriptBuilder.create('dswsclient', 'edu.sc.seis.seisFile.dataSelectWS.Client', task.project, dExtras:dExtras, bat:doBat)
    ScriptBuilder.create('cwbclient', 'edu.sc.seis.seisFile.usgsCWB.Client', task.project, dExtras:dExtras, bat:doBat)
    ScriptBuilder.create('lissclient', 'edu.sc.seis.seisFile.liss.Client', task.project, dExtras:dExtras, bat:doBat)
    ScriptBuilder.create('winstonclient', 'edu.sc.seis.seisFile.winston.WinstonClient', task.project, dExtras:dExtras, bat:doBat)
    ScriptBuilder.create('stationxmlclient', 'edu.sc.seis.seisFile.stationxml.StationXMLClient', task.project, dExtras:dExtras, bat:doBat)
    ScriptBuilder.create('waveserverclient', 'edu.sc.seis.seisFile.waveserver.WaveServerClient', task.project, dExtras:dExtras, bat:doBat)
    ScriptBuilder.create('syncfilecompare', 'edu.sc.seis.seisFile.syncFile.SyncFileCompare', task.project, dExtras:dExtras, bat:doBat)
    ScriptBuilder.create('syncfile2gmt', 'edu.sc.seis.seisFile.syncFile.GMTSyncFile', task.project, dExtras:dExtras, bat:doBat)
}

def getDExtras() {
    return [ 'seisFile':getVersion()]
}



assemble.dependsOn(tarDist)

